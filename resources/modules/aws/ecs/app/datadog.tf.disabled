data "datadog_role" "admins" {
  filter = "Datadog Admin Role"
}

resource "datadog_monitor" "container_oom" {
  for_each = local.services

  name             = "${each.value.family} OOM Alert"
  message          = "The container ${each.value.family} is reporting out of memory errors. ${local.notification_recipients}"
  type             = "event-v2 alert"
  query            = "events(\"source:docker oom \"ecs-${each.value.family}\"\").rollup(\"count\").last(\"1h\") >= 3"
  restricted_roles = [data.datadog_role.admins.id]
}

resource "datadog_monitor" "container_healthcheck" {
  for_each = local.services

  name             = "${each.value.family} Healthcheck Failure Alert"
  message          = "The ECS service ${each.value.family} is reporting ALB healthcheck failures. This is typically caused by a bad deploy but could also indicate some other issue. ${local.notification_recipients}"
  type             = "event-v2 alert"
  query            = "events(\"source:amazon_ecs unhealthy \"${each.value.family}\"\").rollup(\"count\").last(\"30m\") >= 2"
  restricted_roles = [data.datadog_role.admins.id]
}

resource "datadog_monitor" "deploy_failure" {
  name             = "${var.identifier} ${var.env} ECS Deploy Failure Alert"
  message          = "Build number {{cipipeline.attributes.build_number}} for the app ${var.identifier}-${var.env} failed to deploy to ECS. The url for this deploy action is {{cipipeline.attributes.ci.pipeline.url}} {{#is_alert}}${local.notification_recipients}{{/is_alert}}"
  type             = "ci-pipelines alert"
  query            = "ci-pipelines(\"ci_level:pipeline @ci.pipeline.name:\\\"Promote ECS\\\" @ci.status:error @service:${var.identifier} @environment:${local.environment}\").rollup(\"count\").last(\"5m\") >= 1"
  restricted_roles = [data.datadog_role.admins.id]
}
